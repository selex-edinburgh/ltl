grammar com.leonardo.ltl.LTL with org.eclipse.xtext.common.Terminals

import "http://www.leonardo.com/ltl/LTL"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	{Model} packages+=(Package)*;

Package:
	'package' name=QualifiedName
	(
		('description' description=STRING)? &
		((packagableElements+=PackagableElement)*)
	)
	'end';

PackagableElement returns PackagableElement:
	TestExecution | TestableEntity | TestRequirement | TestSpecification;

TestSpecification returns TestSpecification:
	TestSuite | TestCase;

TestableEntity:
	{TestableEntity}
	'entity' name=QualifiedName
	(
		('description' description=STRING)? &
		('version' version=STRING)?
	)
	'end';

TestRequirement:
	{TestRequirement}
	'requirement' name=QualifiedName
	(
		('description' description=STRING)? &
		('version' version=STRING)? &
		('entity' testableEntity=[TestableEntity|QualifiedName])?
	)
	'end';

TestSuite:
	{TestSuite}
	'suite' name=QualifiedName
	(
		('description' description=STRING)? & 
		('date' date=STRING)? &
		('version' version=STRING)? &
		('cases'
		(testSpecifications+=[TestSpecification|QualifiedName])*
		'end') &
		('entities'
			(testableEntities+=[TestableEntity|QualifiedName])*
		'end')
	)
	'end';


TestCase:
	{TestCase}
	'case' name=QualifiedName
	(
		('description' description=STRING)? & 
		('date' date=STRING)? &
		('version' version=STRING)? &
		('precondition' precondition=STRING)? &
		('method' testingMethod=STRING)? &
		('expectedResult' expectedResult=STRING)? &
		('actualResult' actualResult=STRING)? &
		('passed' passed=Boolean)? &
		('incident' incident=STRING)? &
		('postcondition' postcondition=STRING)? &
		('inputs' inputs+=STRING ("," inputs+=STRING)*)? &
		('entities'
			(testableEntities+=[TestableEntity|QualifiedName])*
		'end')
	)	
	'end';

TestExecution:
	'test' name=QualifiedName (
		('description' description=STRING)? &
		('date' date=STRING)? &
		('specification' (testSpecifications+=[TestSpecification|QualifiedName])* 'end')
	)
	'end';

QualifiedName hidden():
	ID ('.' ID)*;

Boolean returns ecore::EBoolean:
	'true' | 'false';